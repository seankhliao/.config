#!/bin/zsh
# ==================== KeyBindings ===================
sudo-command-line() {
    [[ -z $BUFFER ]] && zle up-history
    if [[ $BUFFER == sudo\ * ]]; then
        LBUFFER="${LBUFFER#sudo }"
    else
        LBUFFER="sudo $LBUFFER"
    fi
}
zle -N sudo-command-line
zle -N history-substring-search-up
zle -N history-substring-search-down
# Defined shortcut keys: [Esc] [Esc]
bindkey -e
bindkey "\e\e" sudo-command-line
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
bindkey '^[[H' beginning-of-line
bindkey '^[[4~' end-of-line
bindkey '^[[F' end-of-line

# =================== ZSH Options ==================
setopt AUTOCD

setopt COMPLETE_IN_WORD
setopt HASH_LIST_ALL

setopt EXTENDED_GLOB
setopt GLOB_DOTS

setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_DUPS
setopt HIST_FIND_NO_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_NO_FUNCTIONS
setopt HIST_NO_STORE
setopt HIST_REDUCE_BLANKS
setopt HIST_SAVE_NO_DUPS
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY

setopt ALIASES
setopt CORRECT

# =================== ZSH Completion =================
zstyle ':completion:*' menu select
zstyle ':completion:*' rehash true
zstyle ':completion:*' list-colors "${(@s.:.)LS_COLORS}"
zstyle ':completion::complete:*' gain-privileges 1
zstyle ':completion:*' matcher-list '' \
    '+m:{a-z}={A-Z}' '+m:{A-Z}={a-z}' \
    'r:[^[:alpha:]]||[[:alpha:]]=** r:|=* m:{a-z\-}={A-Z\_}' \
    'r:|?=** m:{a-z\-}={A-Z\_}'
autoload -Uz compinit
compinit -d "$XDG_RUNTIME_DIR"/zcompdump

# ===================== Aliases ===================
alias -s go='go run'
alias -s py='python3'


alias ..='cd ..'
alias ...='cd ../..'
alias ....='cd ../../..'
alias .....='cd ../../../..'
alias ......='cd ../../../../..'
alias .......='cd ../../../../../..'

alias cdr='cd -P .'
alias cr='google-chrome-unstable'

alias diff='diff --color=always'
alias diskspace="df -P -kHl"
alias filespace='du -d 1 -h . 2>/dev/null | sort -h -r'

alias forget="sed -i '$ d' $HISTFILE ; sed -i '$ d' $HISTFILE"

alias g='git'

# create aliases for git
# g<alias> where <alias> is defined in git/config
local start_alias=false
while read line; do
    if ! $start_alias; then
        if [[ $line =~ '\s*\[alias\]' ]]; then
            start_alias=true
        fi
    else
        if [[ $line =~ '\s*\[[a-z]*\]' ]]; then
            break
        fi
        sub=${${line%%=*}// /}
        [[ $sub ]] && alias g${sub}="git ${sub}"
    fi
done < $XDG_CONFIG_HOME/git/config

function gsall() {
    bold=$(tput bold)
    normal=$(tput sgr0)

    for dir in ~/code/*; do
        if [ -d "$dir" ]; then
            if [ -d "$dir"/.git ]; then
                echo "${bold}$dir${normal}"
                cd $dir
                git status -s
                echo ""
                cd ..
            fi
        fi
    done
}
alias gmail-filters-upload="gmailfilters $XDG_CONFIG_HOME/gmail/filters.toml"
alias gmail-filters-edit="nvim $XDG_CONFIG_HOME/gmail/filters.toml"

alias h='htop'
alias l='ls -lGh'
alias ll='ls -alGh'
alias ls='ls --color'


alias py='python3'
alias py2='python3'

alias s='ssh'
alias sd='sudo systemctl'

alias start-vpn='sudo systemctl start wg-quick@wg0'
alias stop-vpn='sudo systemctl stop wg-quick@wg0'

alias t='tag -i'
export TAG_CMD_FMT_STRING="$EDITOR {{.Filename }} +{{ .LineNumber }}"
if (( $+commands[tag] )); then
    tag() {
        command tag "$@";
        source ${TAG_ALIAS_FILE:-/tmp/tag_aliases} 2>/dev/null
    }
fi

if [[ -f '/usr/bin/nvim' ]]; then
    alias v='nvim'
else
    alias v='vim'
fi

alias p='yay'
alias p-update='yay -Syu --noconfirm'
alias p-clean=' yay -Rns $(yay -Qtdq); yay -Sc'

# ==================== Plugins =======================
PLUGIN_HOME=$XDG_DATA_HOME/zsh/plugins

update_plugin () {
    if [[ -d $PLUGIN_HOME/$2 ]] ; then
        git -C $PLUGIN_HOME/$2 pull
    else
        git clone "https://github.com/"$1/$2 $PLUGIN_HOME/$2
    fi
    zcompile $PLUGIN_HOME/$2/$2.zsh
}

update_plugins () {
    if [[ ! -d $PLUGIN_HOME ]] ; then
        mkdir -p $PLUGIN_HOME
    fi

    update_plugin zsh-users zsh-autosuggestions
    update_plugin zsh-users zsh-history-substring-search
    update_plugin zsh-users zsh-syntax-highlighting
    update_plugin seankhliao zsh-prompt
}

plugins=('zsh-autosuggestions' 'zsh-history-substring-search' 'zsh-syntax-highlighting' 'zsh-prompt')
for f in $plugins; do 
    source $PLUGIN_HOME/$f/$f.zsh
done

# The next line updates PATH for the Google Cloud SDK.
if [ -f '/opt/google-cloud-sdk/path.zsh.inc' ]; then source '/opt/google-cloud-sdk/path.zsh.inc'; fi

# The next line enables shell command completion for gcloud.
if [ -f '/opt/google-cloud-sdk/completion.zsh.inc' ]; then source '/opt/google-cloud-sdk/completion.zsh.inc'; fi

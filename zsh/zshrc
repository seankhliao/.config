#!/bin/zsh
# ==================== Profiler ============================
PROFILE=0
_time=$(date +%N)
function _profile () { [[ $PROFILE -ne 0 ]] && echo "$1: $(( ($(date +%N) - ${_time}) / 1000000 ))ms" && _time=$(date +%N) }

# ==================== Autoload Functions ==================
# https://unix.stackexchange.com/questions/33255/how-to-define-and-load-your-own-shell-function-in-zsh
#
# naming:
#   _underscore_prefix: internal
#   normal-dashed: to be called by user
#
# call zcompfuncs to recompile after changing
fpath=( 
    "$ZDOTDIR/funcs"
    "${fpath[@]}" 
)
funcs=(
    _auto_git_aliases
    _kubectl_complete
    _prompt
    _source_plugins
    _sudo_cmdline
    _update_plugin
    colortest
    gsall
    keyless-ssh
    md
    t
    wifi-add
    z-update-plugins
    zcompfuncs
)
autoload -Uz ${funcs}

_profile "autoload"

# ==================== KeyBindings ===================
zle -N _sudo_cmdline
zle -N history-substring-search-up
zle -N history-substring-search-down
# Defined shortcut keys: [Esc] [Esc]
bindkey -e
bindkey "\e\e" _sudo_cmdline
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
bindkey '^[[H' beginning-of-line
bindkey '^[[4~' end-of-line
bindkey '^[[F' end-of-line

_profile "bindkey"

# =================== ZSH Options ==================
setopt AUTOCD
setopt IGNORE_EOF

setopt EXTENDED_GLOB
setopt GLOB_DOTS

setopt AUTO_LIST
setopt AUTO_MENU
setopt AUTO_PARAM_SLASH
setopt COMPLETE_IN_WORD
setopt HASH_LIST_ALL

setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_DUPS
setopt HIST_FIND_NO_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_NO_FUNCTIONS
setopt HIST_NO_STORE
setopt HIST_REDUCE_BLANKS
setopt HIST_SAVE_NO_DUPS
setopt SHARE_HISTORY

setopt ALIASES
setopt CORRECT

_profile "setopt"

# =================== ZSH Completion =================
zstyle ':completion::complete:*' gain-privileges 1
zstyle ':completion:*' group-name ''
zstyle ':completion:*' menu select
zstyle ':completion:*' rehash true
zstyle ':completion:*' list-colors "${(@s.:.)LS_COLORS}"
zstyle ':completion:*' matcher-list '' \
    '+m:{a-z}={A-Z}' '+m:{A-Z}={a-z}' \
    'r:[^[:alpha:]]||[[:alpha:]]=** r:|=* m:{a-z\-}={A-Z\_}' \
    'r:|?=** m:{a-z\-}={A-Z\_}'
autoload -Uz compinit
compinit -d "$XDG_RUNTIME_DIR"/zcompdump

autoload -Uz url-quote-magic
zle -N self-insert url-quote-magic

_profile "completion"

# ==================== gcloud ======================
# The next line updates PATH for the Google Cloud SDK.
[[ -f '/opt/google-cloud-sdk/path.zsh.inc' ]] &&  source '/opt/google-cloud-sdk/path.zsh.inc'
# The next line enables shell command completion for gcloud.
[[ -f '/opt/google-cloud-sdk/completion.zsh.inc' ]] && source '/opt/google-cloud-sdk/completion.zsh.inc'

_profile "gcloud"

# ================= Prompt =====================
_prompt

_profile "prompt"

# ==================== Plugins =======================
local PLUGIN_HOME=$XDG_DATA_HOME/zsh/plugins
local plugins=(
    'zsh-autosuggestions' 
    'zsh-history-substring-search' 
    'zsh-syntax-highlighting'
)
_source_plugins

_profile "plugins"

# ===================== Aliases ===================
# suffix aliases
# open files ending in X with these programs
alias -s go='go run'
alias -s py='python3'

# cd with autocomplete: c <tab>
c () { cd ~/seankhliao/"$1" }
compctl -W ~/seankhliao/ -/ c

# go up
alias ..='cd ..'
alias ...='cd ../..'
alias ....='cd ../../..'
alias .....='cd ../../../..'
alias ......='cd ../../../../..'
alias .......='cd ../../../../../..'

# set default opts
alias cp='cp -v'
alias diff='diff --color=always'
alias grep='grep --color=auto'
alias ln='ln -v'
alias ls='ls --color'
alias mv='mv -v'
alias sudo='sudo --preserve-env'

# short
alias cr='google-chrome-unstable'
alias g='git'
_auto_git_aliases
alias h='htop'
alias kaf='kubectl apply -f'
alias kdf='kubectl delete -f'
alias kdp='kubectl delete po -l'
# alias l='ls -lGh'
alias l='exa -l --git'
# alias ll='ls -alGh'
alias ll='exa -la --git'
alias p='yay --pacman powerpill'
alias py='python3'
alias py2='python3'
alias s='ssh'
alias sd='sudo systemctl'
alias sdu='systemctl --user'
alias tag='t'
alias tree='exa -T'
alias v='vim'; [[ -f '/usr/bin/nvim' ]] && alias v='nvim'

# convenience
cb () { gcloud builds list --limit=5 --filter='source.repo_source.repo_name="github_seankhliao_'$(basename $(git rev-parse --show-toplevel))'"' }
alias cdr='cd -P .'
alias diskspace="df -P -kHl"
alias filespace='du -d 1 -h . 2>/dev/null | sort -h -r'
alias forget="sed -i '$ d' $HISTFILE ; sed -i '$ d' $HISTFILE"
alias gmail-filters-upload="gmailfilters $XDG_CONFIG_HOME/gmail/filters.toml"
alias gmail-filters-edit="nvim $XDG_CONFIG_HOME/gmail/filters.toml"
alias p-update='yay --pacman powerpill -Syu --noconfirm'
alias p-clean=' yay -Rns $(yay -Qtdq); yay -Sc'
alias sd-failed='sudo systemctl list-units --failed'
alias start-vpn='sudo systemctl start wg-quick@wg0'
alias stop-vpn='sudo systemctl stop wg-quick@wg0'
alias wifi-restart="sudo systemctl restart wpa_supplicant@wlp58s0.service"

_profile "aliases"

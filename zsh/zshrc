#!/bin/zsh

# seankhliao's zshrc
# sections begin with 20x =


# ==================== zshrc profiler
# at the emd of each section
# set PROFILER=1
# usage:
#   _profiler "section name"
#
local PROFILER=0
local _time=$(date +%N)
_profiler () { [[ $PROFILER -ne 0 ]] && echo "$1: $(( ($(date +%N) - ${_time}) / 1000000 ))ms" && _time=$(date +%N) }


# ==================== zsh keybindings
#
zle -N _sudo_cmdline
zle -N history-substring-search-up
zle -N history-substring-search-down
# Defined shortcut keys: [Esc] [Esc]
bindkey -e
bindkey "\e\e" _sudo_cmdline
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
bindkey '^[[H' beginning-of-line
bindkey '^[[4~' end-of-line
bindkey '^[[F' end-of-line

_profiler "zsh keybinders"


# =================== zsh options
#
setopt AUTOCD
setopt IGNORE_EOF

setopt EXTENDED_GLOB
setopt GLOB_DOTS

setopt AUTO_LIST
setopt AUTO_MENU
setopt AUTO_PARAM_SLASH
setopt COMPLETE_IN_WORD
setopt HASH_LIST_ALL

setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_DUPS
setopt HIST_FIND_NO_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_NO_FUNCTIONS
setopt HIST_NO_STORE
setopt HIST_REDUCE_BLANKS
setopt HIST_SAVE_NO_DUPS
setopt SHARE_HISTORY

setopt ALIASES
setopt CORRECT

_profiler "zsh options"


# =================== zsh completions
#
zstyle ':completion::complete:*' gain-privileges 1
zstyle ':completion:*' group-name ''
zstyle ':completion:*' menu select
zstyle ':completion:*' rehash true
zstyle ':completion:*' list-colors "${(@s.:.)LS_COLORS}"
zstyle ':completion:*:*:-command-:*:*' ignored-patterns '_*'
zstyle ':completion:*' matcher-list '' \
    '+m:{a-z}={A-Z}' '+m:{A-Z}={a-z}' \
    'r:[^[:alpha:]]||[[:alpha:]]=** r:|=* m:{a-z\-}={A-Z\_}' \
    'r:|?=** m:{a-z\-}={A-Z\_}'
autoload -Uz compinit
compinit -d "$XDG_RUNTIME_DIR"/zcompdump

autoload -Uz url-quote-magic
zle -N self-insert url-quote-magic


[[ -f '/opt/google-cloud-sdk/path.zsh.inc' ]] &&  source '/opt/google-cloud-sdk/path.zsh.inc'
[[ -f '/opt/google-cloud-sdk/completion.zsh.inc' ]] && source '/opt/google-cloud-sdk/completion.zsh.inc'

_profiler "zsh completion"


# =================== autoload functions
# https://unix.stackexchange.com/questions/33255/how-to-define-and-load-your-own-shell-function-in-zsh
#
# lazily loaded function per file
# with cached compilations
#
# naming:
#       _underscore_prefix: internal functions
#       kebab-casing: user callable
#
# recompile:
#       z-comp-funcs
#
fpath=( "$ZDOTDIR"/funcs "${fpath[@]}" )
funcs=(
    _auto_git_aliases
    _kubectl_complete
    _prompt
    _source_plugins
    _sudo_cmdline
    colortest
    gsall
    keyless-ssh
    man
    md
    t
    z-update-plugins
    z-comp-funcs
)
autoload -Uz ${funcs}

_profiler "autoload funcs"


# =================== dockerfuncs
# https://github.com/jessfraz/dockerfiles
#
# run things in docker instead of natively
#
source "$ZDOTDIR"/dockerfuncs

_profiler "dockerfuncs"


# =================== zsh plugins
#
local PLUGIN_HOME=$XDG_DATA_HOME/zsh/plugins
local plugins=(
    'zsh-autosuggestions'
    'zsh-history-substring-search'
    'zsh-syntax-highlighting'
)
_source_plugins

_profiler "zsh plugins"


# =================== zsh prompt
#
_prompt

_profiler "zsh prompt"


# ==================== zsh aliases
# suffix aliases
# usage:
#       runs files with these programs
alias -s go='go run'
alias -s py='python3'


# go up
alias ..='cd ..'
alias ...='cd ../..'
alias ....='cd ../../..'
alias .....='cd ../../../..'
alias ......='cd ../../../../..'
alias .......='cd ../../../../../..'

# set default opts
alias cp='cp -v'
alias diff='diff --color=always'
alias grep='grep --color=auto'
alias ln='ln -v'
alias ls='ls --color'
alias mv='mv -v'
alias sudo='sudo --preserve-env'

# short
alias cdr='cd -P .'
alias cr='google-chrome-unstable'
alias diskspace="df -P -kHl"
alias drun='docker run --rm -it -v $(pwd):/workspace '
alias filespace='du -d 1 -h . 2>/dev/null | sort -h -r'
alias g='git'

_auto_git_aliases

alias h='htop'
alias kaf='kubectl apply -f'
alias kdf='kubectl delete -f'
alias kdp='kubectl delete po -l'
alias l='exa -l --git --time-style iso'
alias ll='exa -la --git --time-style iso'
alias p='yay'
alias py='python3'
alias py2='python2'
alias p-update='yay -Syu --noconfirm'
alias p-clean=' yay -Yc'
alias s='ssh'
alias sd='sudo systemctl'
alias sdu='systemctl --user'
alias tag='t'
alias tree='exa -T'
alias v='vim'; [[ -f '/usr/bin/nvim' ]] && alias v='nvim'

_profiler "zsh aliases"
